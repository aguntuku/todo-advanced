project(':web') {
    apply plugin: 'war'
    apply plugin: 'jetty'
	apply from: "$rootDir/gradle/publishMaven.gradle"
	apply from: "$rootDir/gradle/functionalTest.gradle"
	
	group = 'com.manning.gia'

    dependencies {
	    compile project(':repository')
	    providedCompile 'javax.servlet:servlet-api:2.5'
	    runtime 'javax.servlet:jstl:1.1.2'
    }

	task createBuildInfoFile << {
		def buildInfoFile = new File("$buildDir/build-info.properties")
		Properties props = new Properties()
		props.setProperty('version', project.version)
		props.setProperty('timestamp', new Date().format('yyyy/MM/dd HH:mm:ss'))
		
		def buildNumber = System.env.SOURCE_BUILD_NUMBER
		
		if(buildNumber) {
			props.setProperty('buildnumber', buildNumber)
		}
		
		props.store(buildInfoFile.newWriter(), null)
	}
	
	war {
		dependsOn createBuildInfoFile
	   	baseName = 'todo'
	
		from(buildDir) {
			include 'build-info.properties'
		    into('WEB-INF/classes')
		}
	}
}