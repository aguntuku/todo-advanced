apply plugin: 'sonar-runner'

sonarRunner {
    sonarProperties {
        property 'sonar.projectName', 'todo'
        property 'sonar.projectDescription', 'A task management application'
    }
}

allprojects {
    apply plugin: 'idea'
    group = 'com.manning.gia'
	apply from: "$rootDir/gradle/versioning.gradle"
}

subprojects {
    apply plugin: 'java'
    apply from: "$rootDir/gradle/jacoco.gradle"

    repositories {
        mavenCentral()
    }

    test {
        testResultsDir = file("$testResultsDir/unit")
    }

    sonarRunner {
        sonarProperties {
            property 'sonar.sourceEncoding', 'UTF-8'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.5'
}

task loadConfiguration {
	def configFile = file("$rootDir/gradle/config/buildConfig.groovy")
	def parsedConfig = new ConfigSlurper().parse(configFile.toURL())
	
	allprojects {
		ext.config = parsedConfig
	}
}